buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'
apply plugin: "java"

version = "1.12.2-1.10.27"
group= "hellfirepvp.astralsorcery"
archivesBaseName = "astralsorcery"
String build_version = ""

// Most likely inside Jenkins. Or homebrew environment.
// I hope you know what you do if it's the latter.
if (System.getenv("BUILD_NUMBER") != null) {
    build_version = version + "-v" + System.getenv("BUILD_NUMBER")
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    useDepAts = true

    mappings = "snapshot_20180415"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileApiJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {

    maven {
        // JEI files
        // https when
        url = "http://dvs1.progwml6.com/files/maven"
    }

    maven {
        // CraftTweaker
        name 'jared maven'
        url "https://maven.blamejared.com/"
    }

    maven {
        //Baubles
        name 'thiakil'
        url 'https://maven.thiakil.com'
    }

    maven {
        // Gamestages stuff
        url 'https://maven.mcmoddev.com'
    }

    maven {
        // CTM
        url 'https://maven.tterrag.com'
    }

}

dependencies {

    def mc_version = "1.12"
    def jei_mc_version = "1.12.2"
    def ctm_mc_version = "MC1.12.2"
    def ct_mc_version = "MC1120"

    def jei_version = "4.8.5.151"
    def ct_version = "4.1.8.9"
    def ctm_version = "0.3.3.22"

    deobfCompile "mezz.jei:jei_${jei_mc_version}:${jei_version}"

    deobfCompile("CraftTweaker2:CraftTweaker2-${ct_mc_version}-Main:${mc_version}-${ct_version}")

    deobfCompile "team.chisel.ctm:CTM:${ctm_mc_version}-${ctm_version}"

    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"

    deobfCompile "net.darkhax.gamestages:GameStages-1.12.2:2.0.91"

    deobfCompile "net.darkhax.bookshelf:Bookshelf-1.12.2:2.3.543"

}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

//Sign jar from flatfile if present - local build
task signMainJarFile(type: SignJar, dependsOn: reobfJar) {
    onlyIf { // Skip the task if our secret data isn't available
        project.hasProperty('keyStore')
    }
    if (project.hasProperty('keyStore')) { // This needs to be a path to the keystore file
        keyStore = project.keyStore
        alias = project.alias
        storePass = project.storePass
        keyPass = project.keyPass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}
build.dependsOn signMainJarFile

jar {
    manifest {
        attributes 'FMLAT': 'astralsorcery_at.cfg'
        attributes 'FMLCorePlugin': "hellfirepvp.astralsorcery.core.AstralCore"
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task srcJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = 'sources'
    archiveName = "${baseName}-${version}-sources.${extension}"
}

artifacts {
    archives deobfJar, srcJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version build_version
            from components.java

            artifact deobfJar {
                classifier 'deobf'
            }
            artifact srcJar {
                classifier 'sources'
            }
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("LOCAL_MAVEN")
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": "1.12"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
